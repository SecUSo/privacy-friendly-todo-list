plugins {
    id "com.android.application"
    id "kotlin-android"
    id "com.google.devtools.ksp"
}

def pfaFile = rootProject.file("pfa.properties")

android {

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildFeatures {
        buildConfig = true
    }

    signingConfigs {
        if (pfaFile.exists()) {
            "pfa-key" {
                def pfa = new Properties()
                pfa.load(new FileInputStream(pfaFile))

                storePassword pfa["storePassword"]
                keyAlias pfa["keyAlias"]
                keyPassword pfa["keyPassword"]
                storeFile file(pfa["storeFile"])
            }
        }
    }

    defaultConfig {
        applicationId "org.secuso.privacyfriendlytodolist"
        minSdkVersion 21
        targetSdkVersion 35
        compileSdk 35
        versionCode 12
        versionName "3.1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
        }
    }

    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def appName = "pfa-todo-list"
            outputFileName = appName + "-${variant.name}-v${variant.versionName}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }

        debug {
            debuggable true
            if (pfaFile.exists()) {
                signingConfig signingConfigs."pfa-key"
            }
        }
    }

    lint {
        lintConfig = file("lint.xml")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_21.toString()
    }

    kotlin {
        jvmToolchain(21)
    }

    namespace "org.secuso.privacyfriendlytodolist"
}

dependencies {
    implementation project(path: ":backup-api")
    implementation fileTree(include: ["*.jar"], dir: "libs")
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "com.google.android.material:material:1.12.0"
    implementation "com.google.android.flexbox:flexbox:3.0.0"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.core:core-splashscreen:1.0.1"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test:runner:1.6.2"
    androidTestImplementation "androidx.test.ext:junit:1.2.1"

    def work_version = "2.10.0"
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"

    implementation "androidx.core:core-ktx:1.15.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "androidx.sqlite:sqlite-ktx:2.4.0"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7"

    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-common:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
}

repositories {
    mavenCentral()
}
