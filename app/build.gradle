apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def pfaFile = rootProject.file('pfa.properties')

android {

    signingConfigs {
        if (pfaFile.exists()) {
            'pfa-key' {
                def pfa = new Properties()
                pfa.load(new FileInputStream(pfaFile))

                storePassword pfa['storePassword']
                keyAlias pfa['keyAlias']
                keyPassword pfa['keyPassword']
                storeFile file(pfa['storeFile'])
            }
        }
    }

    compileSdkVersion 32

    defaultConfig {
        applicationId "org.secuso.privacyfriendlytodolist"
        minSdkVersion 17
        targetSdkVersion 32
        versionCode 9
        versionName "2.4.2"
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "pfa-todo-list-${variant.name}-v${variant.versionName}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation'
            }
        }

        debug {
            debuggable true
            if (pfaFile.exists()) {
                signingConfig signingConfigs.'pfa-key'
            }
        }
    }
    lint {
        checkReleaseBuilds false
    }

}

dependencies {
    implementation project(path: ':backup-api')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    def work_version = "2.7.1"
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"

    implementation "androidx.core:core-ktx:1.8.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "androidx.sqlite:sqlite-ktx:2.2.0"
}
repositories {
    mavenCentral()
}
